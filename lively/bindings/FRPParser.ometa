ometa FRPParser <: BSOMetaJSParser {
    isKeyword :x = ?this._isKeyword(x),
    primExpr    = primExpr:p (
                      "fby" expr:b  ("on" expr | (empty -> null)):t -> [#fby, t, p, b]
                    | "[" expr:i "]"                                -> [#getp, i, p]
                    | "." "name":m "(" listOf(#expr, ','):as ")"    -> [#send, m, p].concat(as)
                    | "." "name":f                                  -> [#getp, [#string, f], p]
                    | "(" listOf(#expr, ','):as ")"                 -> [#call, p].concat(as)
                 )
               | primExprHd,
    primExprHd  = "(" expr:e ")"                                    -> e
                | "this"                                            -> [#this]
                | "name":n exactly("'")                             -> [#getLast, n]
                | exactly(":") "name":n                             -> [#gget, n]
                | "name":n                                          -> [#get, n]
                | "number":n                                        -> [#number, n]
                | "string":s                                        -> [#string, s]
                | "function" funcRest
                | "new" ("name":n ('.' | empty) -> n)*:name "(" listOf(#expr, ','):as ")"
                                                                    -> [#new,
                                                                        name.join('.')].concat(as)
                | "[" listOf(#expr, ','):es "]"                     -> [#arr].concat(es)
                | json
                | re,
    strmDefStr  = "name":n spaces ``<-'' <orExpr>:str                        -> [#strmDef, n, str],
    strmDefs    = strmDefStr*
};
